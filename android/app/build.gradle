apply plugin: "com.android.application"
apply plugin: "kotlin-android"
apply from: "$flutterRoot/packages/flutter_tools/gradle/flutter.gradle"
apply plugin: "com.google.gms.google-services"
apply plugin: "com.google.firebase.firebase-perf"
apply plugin: "com.google.firebase.crashlytics"

def localProperties = new Properties()
rootProject.file('local.properties').withInputStream { localProperties.load(it) }

def keystoreProperties = new Properties()
def keystoreFile = rootProject.file('key.properties')

if (keystoreFile.exists()) {
    keystoreFile.withInputStream { keystoreProperties.load(it) }
}

def flutterVersionCode = localProperties.getProperty('flutter.versionCode', "1")
def flutterVersionName = localProperties.getProperty('flutter.versionName', "1.0.0")
def buildNumber = System.getenv("BUILD_NUMBER") ?: "0"

android {
    namespace "com.evanlu.possystem"
    compileSdkVersion flutter.compileSdkVersion
    ndkVersion flutter.ndkVersion

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    defaultConfig {
        applicationId "com.evanlu.possystem"
        minSdkVersion 23
        targetSdkVersion 33
        multiDexEnabled true
        versionCode flutterVersionCode.toInteger()
        versionName flutterVersionName
    }

    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile keystoreProperties['storeFile'] ? file(keystoreProperties['storeFile']) : null
            storePassword keystoreProperties['storePassword']
            storeType keystoreProperties['storeType']
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            debuggable true
        }
    }

    flavorDimensions "mode"

    productFlavors {
        prod {
            dimension "mode"
        }
        dev {
            dimension "mode"
            versionNameSuffix "-dev+" + buildNumber
        }
    }

    lintOptions {
        disable 'InvalidPackage'
        checkReleaseBuilds false
    }
}

flutter {
    source '../..'
}

dependencies {
    implementation 'com.android.support:multidex:1.0.3'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.9.25"
    implementation "androidx.multidex:multidex:2.0.1"
}
